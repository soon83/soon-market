rootProject.name = 'soon-market'

// 멀티모듈을 쉽게 만들기 위한 설정
["server", "common", "web", "test"].each {
    def compDir = new File(rootDir, it)
    if (!compDir.exists()) {
        compDir.mkdirs()
    }

    compDir.eachDir { subDir ->
        def gradleFile = new File(subDir.absolutePath, "build.gradle")
        if (!gradleFile.exists()) {
            gradleFile.text =
                    """
                    dependencies {
                    
                    }                
                    """.stripIndent(20)
        }

        [
                "src/main/java/com/soon83",
                "src/main/resources",
                "src/test/java/com/soon83",
                "src/test/resources"
        ].each { srcDir ->
            def srcFolder = new File(subDir.absolutePath, srcDir)
            if (!srcFolder.exists()) {
                srcFolder.mkdirs()
            }
        }

        def projectName = ":${it}-${subDir.name}";
        include projectName
        project(projectName).projectDir = subDir
    }
}
