buildscript {
    ext {
        springBootVersion = '2.6.6'
        springCloudVersion = '2021.0.1'
        boot = 'org.springframework.boot'
        cloud = 'org.springframework.cloud'
        lombok = 'org.projectlombok:lombok'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "$boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

allprojects {
    group = 'com.soon83'
    version = '0.0.1'
}

subprojects {
    apply plugin: boot
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'
    apply plugin: "idea"

    repositories {
        mavenCentral()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencyManagement {
        imports {
            mavenBom "$cloud:spring-cloud-dependencies:$springCloudVersion"
        }
    }

    dependencies {
        compileOnly "$lombok"
        annotationProcessor "$lombok"
        testImplementation "$boot:spring-boot-starter-test"
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

// 그냥 jar 인 애들
["web", "test"].each {
    def subProjectDir = new File(projectDir, it)
    subProjectDir.eachDir { dir ->
        def projectName = ":${it}-${dir.name}"
        project(projectName) {
            bootJar.enabled(false)
            jar.enabled(true)
        }
    }
}

// bootJar 인 애들
["server"].each {
    def subProjectDir = new File(projectDir, it)
    subProjectDir.eachDir { dir ->
        def projectName = ":${it}-${dir.name}"
        project(projectName) {
        }
    }
}
